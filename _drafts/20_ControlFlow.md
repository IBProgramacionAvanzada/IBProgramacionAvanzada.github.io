---
layout: post
title: Manejando por las ramas 
tagline: Manejando por las ramas con `if`, _pattern matching_ y recursi贸n.
categories: 
- F# as your first functional programming language
tags:
- fsharp
---


Manejando por las ramas con `if`, _pattern matching_ y recursi贸n.

Uno de los conceptos m谩s importantes en programaci贸n es el control de flujo. ste se refiere a la posibilidad del c贸digo de manejar diferentes caminos de ejecuci贸n de acuerdo a condiciones que aparecen al procesar datos. Por ejemplo, el usuario de la m谩quina expendedora puede pagar con billetes, monedas o tarjeta de cr茅dito, y el sistema de software debe poder acomodar todos esos casos.

En la mayor铆a de los lenguajes existe la sentencia `if condici贸n then ... else`, que permite administrar el c贸digo de acuerdo a si la condici贸n es verdadera o falsa. 

Sin embargo, en F#, no tenemos sentencias sino expresiones, con lo cual el `if` tambi茅n es evaluado como una expresi贸n y debe devolver un valor. Por ejemplo, podemos evaluar la funci贸n $\|x\|$:

```fsharp
let absoluteValue x =
    let v = 
        if x < 0 then
            -x 
        else 
            x
    v

printfn "%A" (absoluteValue 4)
printfn "%A" (absoluteValue -5)
```

    4
    5


En este ejemplo, el resultado de la evaluaci贸n de la expresi贸n `if` se guarda en el identificador `v` que es el valor de retorno de la funci贸n. Se puede escribir el `if` en una sola l铆nea:

```fsharp
let absoluteValue2 x =
    let v = if x < 0 then -x else x 
    v
            
printfn "%A" (absoluteValue2 2)
printfn "%A" (absoluteValue2 -8)
```

    2
    8


E incluso no es necesario usar `v`, ya que el condicional se eval煤a y su resultado es el que devuelve la funci贸n:

```fsharp
let absoluteValueOneliner x =
    if x < 0 then -x else x 
    
            
printfn "%A" (absoluteValueOneliner 2)
printfn "%A" (absoluteValueOneliner -8)
```

    2
    8


La expresi贸n `if` no es m谩s que un _edulcorante sint谩ctico_ para una expresi贸n de pattern matching:

```fsharp
let absoluteValuePM x =
    match x < 0 with 
    | true -> -x 
    | false -> x 
    
            
printfn "%A" (absoluteValuePM 2)
printfn "%A" (absoluteValuePM -8)
```

    2
    8


En efecto, la expresi贸n `x < 0` es de tipo `bool`, que tiene dos casos posibles, `true` y `false`.  

Obs茅rvese tambi茅n que ambas ramas del condicional deben evaluarse al mismo tipo de dato, en el ejemplo anterior, `int`. 

El lenguaje no provee otra variante del condicional `if`, porque para eso est谩...


### Pattern Matching

Una de las fortalezas del lenguaje radica en la construcci贸n de _pattern matching_. Hasta ahora la hemos usado para seleccionar los casos posibles en una uni贸n discriminada. Sin embargo, el _pattern matching_ es m谩s general y, a diferencia del `if`, permite crear m煤ltiples ramas. Para ello se usa la palabra reservada `when`, de la siguiente manera:

```fsharp
let votingAge = 16
let buyingOHAge = 18 

let checkAge (age: int) =
    match age with 
    | a when a < votingAge -> printfn "No vota ni puede comprar vino"
    | a when a >= votingAge && a < buyingOHAge -> printfn "Puede votar, pero no comprar vino"
    | a when a >= buyingOHAge -> printfn "Puede votar y comprar vino" 


checkAge 15
checkAge 17
checkAge 50
```

    No vota ni puede comprar vino
    Puede votar, pero no comprar vino
    Puede votar y comprar vino


En este caso el _pattern matching_ nos permite decidir entre tres opciones de la edad. Notar que la funci贸n `checkAge` retorna el tipo `unit`. Otro ejemplo ser铆a:

```fsharp
type WeatherWarning = 
    | Freezing 
    | YellowAlertHot
    | RedAlertHot 
    | NoAlarm 

let emitAlarm warning = 
    match warning with 
    | Freezing -> printfn "%A" "Alarm: Freezing"
    | YellowAlertHot-> printfn "%A" "Alarm: Yellow"
    | RedAlertHot -> printfn "%A" "Alarm: Red"
    | NoAlarm  -> printfn "%A" "It's fine "

let checkTempForWarning temp =
    match temp with 
    | t when t <= 0 -> Freezing 
    | t when t > 33 && t < 39 -> YellowAlertHot
    | t when t >= 39 -> RedAlertHot
    | _ -> NoAlarm     

emitAlarm (checkTempForWarning 43)

checkTempForWarning 37
|> emitAlarm 

-3 
|> checkTempForWarning 
|> emitAlarm 


```

    "Alarm: Red"
    "Alarm: Yellow"
    "Alarm: Freezing"


Un ejemplo m谩s para ver c贸mo es la sintaxis del _pattern matching_ en la situaci贸n en que se quieren agrupar casos:

```fsharp
type Animals =
    | Cat
    | Dog 
    | Eagle  

let howManyLegs animal =
    match animal with
    | Animals.Cat | Animals.Dog -> 4  // grouping two cases
    | Animals.Eagle -> 2

printfn "%A" (howManyLegs Animals.Cat)
printfn "%A" (howManyLegs Animals.Dog)
printfn "%A" (howManyLegs Animals.Eagle)

```

    4
    4
    2


Esto es posible en el caso en que el _pattern matching_ sea contra expresiones que son o representan constantes. 

### Reemplazando el `for` por recursi贸n

Dado que el loop `for` habitual requiere (ya sea explicita o impl铆citamente) alg煤n tipo de contador que se va actualizando, esto es, es una _variable_, es necesario contar con una forma de poder hacer loops sin el `for`. Para ello se utiliza la recursi贸n en todos los lenguajes funcionales. 

Por ejemplo, si uno tiene el loop

```c
    for(int i = 0; i < n; i++)
    {
        printf("%d\n",i);
    }
```

Se reemplazar谩 por una funci贸n recursiva:

```fsharp
let printNumbers n = 

    let rec loop i n =
        if i < n then
            printfn "%d" i
            loop (i + 1) n
        else 
            printfn "done"

    loop 0 3

printNumbers 3    
```

    0
    1
    2
    done


El primer punto a notar es que, dado que en el lenguaje hay solo expresiones, los loops `for` van a aparecer representados por funciones, ya no es posible tener un loop como un conjunto de sentencias que se ejecutan. En general los loops como sentencias implican tambi茅n una aplicaci贸n sobre variables de lo que sucede dentro del loop. 



Veamos el cl谩sico ejemplo de la serie de Fibonacci:
$$
a_n = a_{n-1} + a_{n-2}
$$
con $a_0=0$ y $a_1=1$.

Este ejemplo, escrito en C ser铆a:
```c
int fib(int n) {
    int prev1 = 0, prev2 = 1, curr = 0;
    if (n == 0)
        return prev1;
    else if (n == 1)
        return prev2;
    else {
        for (int i = 2; i <= n; i++) {
            curr = prev1 + prev2;
            prev1 = prev2;
            prev2 = curr;
        }
        return curr;
    }
}
```

La versi贸n no recursiva implica guardar el estado de los dos valores previos de la serie dentro de la funci贸n, que se realiza al usar `prev1=0` y `prev2=1`. 

La versi贸n recursiva en C ser铆a 
```c
int fib(int n) {
    if (n < 2)
        return n;
    else
        return fib(n-1) + fib(n-2);
}
```

En F# la recursi贸n funciona en forma similar, aunque se puede utilizar _pattern matching_:

```fsharp
let rec fib n  = 
    match n with
    | 0 | 1 -> n
    | n -> fib (n-1) + fib (n - 2)
    
fib 10 |> printfn "%i" 
```

    55


Uno podr铆a estar interesado en sumar una serie de n煤meros, por ejemplo calcular la serie

$$
1 + \frac{1}{2} + \frac{1}{3} + \cdots + \frac{1}{n}
$$

hasta un cierto valor de $n$:

```fsharp
let rec sumSeries n =
    match n with
    | 0 -> 0.0
    | n -> 1.0/(float n) + sumSeries(n-1)

printfn "%A" (sumSeries 100)    
```

    5.187377518


En la pr谩ctica, la forma m谩s pura de programar una recursi贸n no es la m谩s adecuada. Por ejemplo, en el caso de la sucesi贸n de Fibonacci, hay llamados a la funci贸n recursiva que recalculan valores que efectivamente ya fueron calculados. Por eso se utiliza frecuentemente el m茅todo conocido como _tail recursion_. En este caso, se previene el llamado innecesario a la funci贸n recursiva introduciendo nuevos valores que van llevando la informaci贸n del estado anterior de la recursi贸n al c谩lculo presente:

```fsharp
let fib n =
    let rec loop acc1 acc2 n =
        match n with
        | 0 -> acc1
        | 1 -> acc2
        | _ ->
            loop acc2 (acc1 + acc2) (n - 1)
    loop 0 1 n
```

En este caso la funci贸n exterior `fib` no es recursiva en s铆 misma, pero utiliza una funci贸n recursiva en su interior, denominada `loop`. Los valores `acc1` y `acc2` son los encargados de llevar la informaci贸n de los elementos previos de la serie necesarios para calcular el elemento presente.

>  Es importante recordar que F# es un lenguaje _multiparadigma_, as铆 que, estrictamente hablando, s铆 posee un `for` parecido al que uno conoce en otros lenguajes. En este curso en particular nos estamos enfocando en el paradigma funcional de F#, con lo cual **elegimos** descartar el uso del `for` tradicional.