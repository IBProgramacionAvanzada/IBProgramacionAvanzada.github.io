---
layout: post
title: Discriminated Unions
tagline: Tratar con entidades que pertenecen a un grupo de casos bien predefinido.

categories: 
- F# as your first functional programming language
tags:
- fsharp
---




Uno de los aspectos clave de F\# (y otros lenguajes funcionales) es que proporciona una sintaxis espec칤fica para modelar aquellas caracter칤sticas de entradas y salidas que pertenecen a una colecci칩n bien predefinida. Estos se llaman uniones discriminadas (_discriminated unions_).

El tipo para nuestra `VendingMachine` de comida ser칤a:

```fsharp
type FoodProduct =
    | Chips
    | Chocolate
    | Candy 
```

mientras que para la electr칩nica podr칤amos tener:

```fsharp
type Electronics = 
    | Phones
    | Speakers
    | Headphones
```

> 游댒 Se acostumbra usar `PascalCase` para tipos (es decir, `FoodProduct`).

> 仇勇 Los tipos de casos en una uni칩n discriminada deben comenzar con una letra may칰scula (`Phones`, `Chips`, etc.).

En las expresiones anteriores estamos definiendo un tipo con un nombre (`FoodProduct`, `Electronics`) que puede tener varios tipos de casos. Es importante recalcar que los casos de cada tipo de uni칩n discriminada son _disjuntos_, es decir, no se puede acceder al mismo tiempo. Por ejemplo:

```fsharp
let d = FoodProduct.Chips
let s = Electronics.Speakers
```

El lenguaje usa el punto `.` para representar un caso de uni칩n discriminada. El valor `s` representa, por supuesto, un altavoz, y dado que es inmutable, no hay forma posible de que pueda ser un tel칠fono o un auricular.

Entonces, 쯖칩mo podemos definir funciones con uniones discriminadas? Escribamos una funci칩n 'precio' que recibe una entrada de tipo `Electronics` y nos da el precio:

```fsharp
let price electronic = 
    match electronic with 
    | Phones -> 435
    | Speakers -> 29
    | Headphones -> 122

printfn "The price of speaker is: %A $" (price s)    
```

    The price of speaker is: 29 $


La forma en que uno puede desagregar los diferentes casos de una entrada que es una uni칩n discriminada es a trav칠s de _coincidencia de patrones_ (_pattern matching_), representada por la construcci칩n `match ... with` y luego, **todos** los casos posibles de la uni칩n. La sintaxis es bastante sencilla: para cada etiqueta de caso de uni칩n discriminada (despu칠s del signo `|` en la construcci칩n) aparece la flecha `->` que indica el valor (un `int`) que devuelve la funci칩n en ese caso.

Tenga en cuenta tambi칠n que la coincidencia debe contener _todos_ los posibles casos de la uni칩n discriminada. Si uno est치 olvidando alg칰n caso, el compilador nos lo informar치 con un subrayado ondulado en la coincidencia. Esto significa que la coincidencia de patrones es _exhaustiva_.

Otro aspecto muy importante de la coincidencia de patrones es que se eval칰a _en el orden en que se escribe_.

Pero, 쯤u칠 sucede si queremos escribir una funci칩n `priceFood` y asignar un precio de 1,5 a todos los art칤culos, excepto a los chocolates, que tienen un precio de 2,35? El lenguaje introduce el s칤mbolo _comod칤n_ que coincide con _cualquier_ entrada en la construcci칩n de coincidencia de patrones. El comod칤n est치 representado por el s칤mbolo `_` (gui칩n bajo):

```fsharp
let priceFood food = 
    match food with 
    | Chocolate -> 2.35
    | _ -> 1.5 

printfn "Chocolate: %A $" (priceFood FoodProduct.Chocolate)        
printfn "Chips: %A $" (priceFood FoodProduct.Chips)        
printfn "Candy: %A $" (priceFood FoodProduct.Candy)        
```

    Chocolate: 2.35 $
    Chips: 1.5 $
    Candy: 1.5 $


Aqu칤 vemos la interacci칩n entre la evaluaci칩n del orden de la coincidencia de patrones y el comod칤n. Estamos devolviendo valores espec칤ficos para _algunos casos_ (Chocolate), y asignamos un valor com칰n para _el resto de los casos_. Cuando `priceFood` recibe un "valor" de comida, se compara primero con el caso de "Chocolate". Si no es chocolate, el patr칩n comod칤n lo captura. Considere el siguiente ejemplo:

```fsharp
let priceSale food = 
    match food with 
    | _ -> 1.5 
    | Chocolate -> 2.35

printfn "Chocolate: %A $" (priceSale FoodProduct.Chocolate)        
printfn "Chips: %A $" (priceSale FoodProduct.Chips)        
printfn "Candy: %A $" (priceSale FoodProduct.Candy)        
```

    Chocolate: 1.5 $
    Chips: 1.5 $
    Candy: 1.5 $


Bueno, 춰todo est치 "en oferta" a 1,5! Esto se debe a que el comod칤n captura _cualquier entrada_ y dado que es el primer _caso_, cualquier alimento tendr치 ese precio.

Aqu칤 nuevamente, el compilador detr치s de escena viene a nuestro rescate. Le har치 saber que no se alcanzar치n algunas de las reglas de coincidencia de patrones:

<img src="../img/rule_will_never_match.png" alt="This will save you from bankruptcy" width="400"/>

### Combinando uniones discriminadas y tipos b치sicos

En muchos casos, la construcci칩n de uni칩n discriminada no es lo suficientemente general, por lo que se puede combinar con tipos b치sicos. Digamos que queremos identificar la marca de cada uno de nuestros art칤culos en las m치quinas expendedoras. Dado que puede acomodar m칰ltiples marcas de productos, decidimos que representamos la marca por una "cadena". Podemos expandir nuestro tipo de uni칩n discriminada `FoodProduct` como:

```fsharp
type BrandedFood =
    | Chips of string 
    | Chocolate of string 
    | Candy of string 
```

Cada uno de los casos del tipo `BrandedFood` tiene ahora un _valor_ de tipo `string`. La uni칩n discriminada utiliza la palabra clave `of` para asociar cada caso con cada tipo de valor. Uno puede definir identificadores para este tipo compuesto como:

```fsharp
let belovedChocolate = BrandedFood.Chocolate "Wonka"
let healthyChips = BrandedFood.Chips "NotALeis"
let sourCandy = BrandedFood.Candy "TearDrops"
```

En el ejemplo de la marca, elegimos combinar todos los casos con el mismo tipo b치sico, `string`. Pero, de nuevo, uno puede mezclar y combinar. Por ejemplo, un chocolate puede venir en diferentes presentaciones:

```fsharp
type ChocolatePresentation =
| Bar of float  // a chocolate Bar of a given weight
| Box of int // a package with a number of chocolate pieces    
```

O bien, si queremos modelar el cambio de dinero que la m치quina expendedora devuelve al cliente, podemos definir un caso en el que se devuelve una cantidad real y otro que representa el hecho de que el cliente acaba de poner el dinero exacto en la m치quina:

```fsharp
type Change =
| Amount of float
| NoChange
```

La coincidencia de patrones en funciones contra estas uniones discriminadas compuestas es nuevamente bastante simple. Para obtener la marca de un producto `BrandedFood`, podemos definir la funci칩n `marca`:

```fsharp
let brand product = 
    match product with
    | Chips p -> p 
    | Chocolate p -> p 
    | Candy p -> p  
```

```fsharp
printfn "Brand of belovedChocolate: %s" (brand belovedChocolate)
```

    Brand of belovedChocolate: Wonka


En esta funci칩n, cada caso tiene un valor asociado representado por el identificador `p`, que se _desenvuelve_ de la uni칩n discriminada y se devuelve.

Otro ejemplo:

```fsharp
let changeValue change =
    match change with 
    | Amount money -> money
    | NoChange -> 0 

let c = Amount 3 

printfn "You are receiving %A $ as change" (changeValue c)
```

    You are receiving 3.0 $ as change


> Notar sobre c칩mo una uni칩n discriminada que mezcla tipos b치sicos parece presentar una especie de divergencia en el c칩digo. El tipo `Change` combina un tipo `float` envuelto (con `Amount`) y un tipo de uni칩n pura `NoChange`. Dado que las funciones deben tener tipos de entrada y salida espec칤ficos, cualquier funci칩n que reciba una entrada `Change` tiene que devolver una salida definida. Normalmente hay dos posibilidades. La primera es que la funci칩n _aplane_ las entradas (como en el caso de `changeValue`), donde se obtiene el valor flotante del dinero que recibe el usuario. La segunda corresponde a una funci칩n que promueve los tipos de entrada a otro: por ejemplo, el caso trivial ser칤a una funci칩n que imprime la cantidad de dinero que recibe el cliente, `imprimeCambio: Change -> ()`, es decir, recibe un 'Cambio' y devuelve 'unidad'.
> Esta es una de las ideas clave detr치s de la programaci칩n funcional, poder conectar diferentes tipos a trav칠s de funciones.

### Protecci칩n de entradas con uniones discriminadas 칰nicas

El 칰ltimo, pero no menos importante, caso de uso de uniones discriminadas corresponde a aquellas que solo tienen un t칠rmino. Digamos que necesitamos describir los modelos de los diferentes art칤culos en nuestra m치quina expendedora de productos electr칩nicos. Una forma de hacerlo es usando una uni칩n discriminada _칰nica_ (_single discriminated union_):

```fsharp
type Model =
    | Model of string
```

Entonces, podemos definir diferentes modelos de art칤culos:

```fsharp
let yPhone = Model "Xtreme 3S"
let miniSpeakers = Model "Louder Pro"
```

Dado que la uni칩n discriminada solo tiene un caso, hay una forma breve de desenvolver el valor que contiene:

```fsharp
let (Model yPhoneModel) = yPhone
printfn "The model of the yPhone is %A" yPhoneModel
```

    The model of the yPhone is "Xtreme 3S"


Parece que hay mucha pompa en estos tipos, 쯣or qu칠 no usar una 'cadena' m치s simple en su lugar? La respuesta est치, de nuevo, relacionada con las funciones. Definamos la funci칩n `printModel` como:

```fsharp
let printModel (model: Model) =
    let (Model value) = model 
    printfn "The model is %A" value 
```

```fsharp
printModel yPhone
printModel miniSpeakers
```

    The model is "Xtreme 3S"
    The model is "Louder Pro"


La firma de la funci칩n `printModel` es `Modelo -> ()`, lo que significa que recibe un valor de modelo y devuelve la unidad. Dado que la entrada es un 'Modelo', no hay forma posible de que podamos pasarle una cadena simple. De esta manera, uno protege la entrada de la funci칩n de tal manera que tiene que ser del tipo exacto que decidimos, y nada m치s.

Debe se침alarse que esto no es una validaci칩n, es decir, todav칤a se puede mezclar el significado de 'Modelo' construy칠ndolo con cualquier cadena, la 칰nica pieza de c칩digo que es la funci칩n 'printModel' est치 protegida por el 칰nico tipo de uni칩n discriminada. Entonces, este tipo no reemplaza la validaci칩n, pero le brinda un c칩digo m치s limpio.

### Terminando

El tipo de uni칩n discriminada en ambas variantes (simple y m칰ltiple) es un aspecto clave de la programaci칩n funcional en F\#. La capacidad de describir colecciones bien predefinidas con un solo tipo simplifica los c칩digos y los hace robustos. La especificaci칩n exhaustiva de coincidencia de patrones (supervisada por el compilador F\# entre bastidores) garantiza que el programador se ocupe de todos los casos de la uni칩n discriminada y que no se deje ni un solo caso con posibles agujeros en el c칩digo.

El tipo de uni칩n discriminada es la forma en que F\# representa un _tipo de suma_, como se llama a este tipo de tipos en la teor칤a algebraica de tipos.


